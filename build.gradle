buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        ivy {  url = 'http://repository-javafx-gradle-plugin.forge.cloudbees.com/release' }
    }
    dependencies {
        classpath 'org.bitbucket.shemnon.javafxplugin:gradle-javafx-plugin:0.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'javafx'

group = 'com.artclod'
version = '0.0.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_7
String scalaVer = '2.10'
String scalaVersion = "$scalaVer" + '.0' // NOTE this version may not match the plugin version it needs to be kept up to date manually

javafx {
    id = "SickDays"
    mainClass = 'com.artclod.sickdays.application.LaunchSickDaysApplication'
}

sourceSets {
	example {
		scala { srcDir 'example/scala' }
	}
}

sourceSets.main.scala.srcDir "src/main/java"
sourceSets.main.java.srcDirs = []

task sourcesJar(type: Jar, dependsOn:classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts { archives sourcesJar }

repositories {
	mavenCentral()
	flatDir { dirs 'lib' }
    maven { url = 'http://repo.spray.io' }
}

dependencies {
	compile     group: 'org.apache.commons', name: 'commons-math',          version: '2.2'
	compile	    group: 'com.miglayout',      name: 'miglayout-javafx',      version: '4.2'
	compile     group: 'org.scala-lang',     name: 'scala-library',         version: "$scalaVersion"
	compile     group: '',                   name: 'javafx-artclod',        version: '0.0.1'
	compile     group: 'io.spray',           name: "spray-json_$scalaVer",  version: '1.2.3'
	compile     group: 'org.scalaz',         name: "scalaz-core_$scalaVer", version: '6.0.4'
	
	scalaTools  group: 'org.scala-lang',     name: 'scala-compiler',        version: "$scalaVersion"
	scalaTools  group: 'org.scala-lang',     name: 'scala-library',         version: "$scalaVersion"
                                                                          
	testCompile group: 'junit',              name: 'junit-dep',             version: '4.10'
	testCompile group: 'org.hamcrest',       name: 'hamcrest-core',         version: '1.2.1'
	testCompile group: 'org.mockito',        name: 'mockito-core',          version: '1.8.5'
}

eclipse {
  classpath {
    containers "org.scala-ide.sdt.launching.SCALA_CONTAINER"
    file {
      whenMerged { classpath ->
        def scalaLib = classpath.entries.findAll {it.toString().contains("scala-library")}
        // Avoid problems having two scala-library in the path (the Scala IDE plugin brings one and gradle needs one for non IDE purposes)
        classpath.entries.removeAll(scalaLib)
      }
    }
  }  
}
